apply plugin: 'base'

allprojects {
	version = '1.2'
	ext.title = 'EBC4J API'
	ext.vendor = 'object-zoo - Tilmann Kuhn'
	ext.copy = '2010 - 2014'
	ext.javadoc = 'http://java.sun.com/javase/6/docs/api/'
	ext.encoding = 'UTF-8'
}

subprojects {
	apply plugin: 'java'
	
	dependencies {
	    compile fileTree(dir: '../lib/main', include: '**/*.jar')
		testCompile fileTree(dir: '../lib/test', include: '**/*.jar')
	}
	
	jar	{
		manifest {
			attributes("Implementation-Title": project.title, "Implementation-Version": project.version, "Implementation-Vendor": project.vendor)
		}
	}
	
	task updateTestResultTimestamps {
		dependsOn test
		inputs.files test.outputs.files
		doLast {
			def timestamp = System.currentTimeMillis()
			test.testResultsDir.eachFile { it.lastModified = timestamp }
			test.testReportDir.eachFile { it.lastModified = timestamp }
		}
    }
	
	task jenkinsBuild {
		dependsOn updateTestResultTimestamps
	}
}

project(':ebc4j') {
	jar	{
		from sourceSets.main.allJava
	}
}

project(':ebc4j-async') {
	dependencies {
		compile project(':ebc4j')
		testCompile project(':ebc4j').sourceSets.test.output
	}
}

task javadoc(type: Javadoc) {
    destinationDir = new File("${rootDir}/doc/api")
    source = files(subprojects.collect { project -> project.sourceSets.main.allJava })
    classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
}

task distZip(type: Zip) {
	dependsOn ':ebc4j:build'
	dependsOn ':ebc4j-async:build'
	dependsOn ':javadoc'

	into("${baseName}-${version}") {
		from (rootDir)
		{
			include '*.*'
			exclude { file -> file.directory }
			exclude '*.jar'
		}
		from (rootDir)
		{
			include 'doc/**/*', 'ebc4j/**/*', 'ebc4j-async/**/*', 'lib/**/*'
			exclude '*/**/build', '*/**/generated'
		}
		from (files (subprojects.collect { project -> project.jar.destinationDir }))
		{
			include '*.jar'
		}
	}
}

allprojects {
	javadoc	{
		options {
			setUse(true)
			setAuthor(true)
			setVersion(true) 
			setNoTimestamp(true)
			encoding project.encoding
			links project.javadoc
			overview "${rootDir}/doc/index.html"
			docTitle "<h1>${project.title} ${project.version}</h1>"
			windowTitle "${project.title} ${project.version}"
			bottom "Copyright &copy; ${project.copy} - ${project.vendor}"
		}
	}
}